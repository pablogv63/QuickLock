plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.devtools.ksp' version '1.6.21-1.0.5'
}

def signingConfigFile = rootProject.file("signingConfig.properties")
def signingConfigProperties = new Properties()
signingConfigProperties.load(new FileInputStream(signingConfigFile))

android {
    signingConfigs {
        release {
            storeFile file(signingConfigProperties["STORE_FILE_PATH"])
            storePassword signingConfigProperties["STORE_PASSWORD"]
            keyAlias signingConfigProperties["KEY_ALIAS"]
            keyPassword signingConfigProperties["KEY_PASSWORD"]
        }
    }
    compileSdk 32

    defaultConfig {
        applicationId 'com.pablogv63.quicklock'
        minSdk 28
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    // Compose Destinations
    applicationVariants.all { variant ->
        kotlin.sourceSets {
            getByName(variant.name) {
                kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
            }
        }
    }
}

dependencies {

    /* General app dependencies */
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_ktx_version"
    implementation "androidx.activity:activity-compose:$activity_compose_version"
    testImplementation 'junit:junit:4.13.2'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    /* Material 3 */
    implementation "androidx.compose.material3:material3:$material3_version"
    implementation "androidx.compose.material3:material3-window-size-class:1.0.0-alpha13"
    implementation "com.google.accompanist:accompanist-flowlayout:0.24.8-beta"

    /* Extended material icons */
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    /* Koin dependencies */
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-androidx-navigation:$koin_version"
    implementation "io.insert-koin:koin-androidx-compose:$koin_version"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"

    /* Test dependencies */
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$test_ext_junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    /* Room dependencies */
    implementation "androidx.room:room-ktx:$rootProject.room_version"
    kapt "androidx.room:room-compiler:$rootProject.room_version"

    /* Compose Destinations
    (https://proandroiddev.com/compose-destinations-simpler-and-safer-navigation-in-compose-with-no-compromises-74a59c6b727d)
    (https://composedestinations.rafaelcosta.xyz/) */
    implementation "io.github.raamcosta.compose-destinations:animations-core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    /* Android Security */
    implementation "androidx.security:security-crypto:$security_version"
    // For Identity Credential APIs
    implementation "androidx.security:security-identity-credential:$security_identity_credential_version"
    // For App Authentication APIs
    implementation "androidx.security:security-app-authenticator:1.0.0-alpha02"
    // For App Authentication API testing
    androidTestImplementation "androidx.security:security-app-authenticator:1.0.0-alpha02"


    /* Kotlin CSV
    * (https://medium.com/@tonia.tkachuk/how-to-export-room-database-to-csv-file-a51fec589618)*/
    implementation "com.github.doyaaaaaken:kotlin-csv-jvm:1.3.0"

    /* Compose Preferences
    * (https://github.com/Sh4dowSoul/ComposePreferences) */
    implementation "com.github.Sh4dowSoul.ComposePreferences:preferences-material3:$compose_preferences_version"

    /* GSON */
    implementation "com.google.code.gson:gson:$gson_version"

    /* Color Picker */
    implementation 'com.godaddy.android.colorpicker:compose-color-picker:0.4.2'

    // with Android ColorInt extensions
    implementation 'com.godaddy.android.colorpicker:compose-color-picker-android:0.4.2'
}